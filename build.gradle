buildscript {
  ext.kotlin_ver = '1.0.0'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"
  }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.kt3k'
version = '1.0.0'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_ver"
  compile gradleApi()
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  repositories {
    maven {
      credentials {
        username project.'sonatype.username'
        password project.'sonatype.password'
      }
      url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
  }

  publications {
    maven(MavenPublication) {
      groupId 'org.kt3k'
      artifactId 'ebean-enhance-plugin'
      version '1.0.0'

      from components.java

      artifact sourceJar {
        classifier 'sources'
      }

      pom.withXml {

        asNode().children().last + {

          resolveStrategy = Closure.DELEGATE_FIRST

          name 'ebean-enhance-plugin'
          description 'A gradle plugin for enhancing ebean entities.'
          url 'https://github.com/kt3k/ebean-enhance-plugin'
          scm {
            url 'scm:git+https://github.com/kt3k/ebean-enhance-plugin.git'
            connection 'scm:git+https://github.com/kt3k/ebean-enhance-plugin.git'
            developerConnection 'scm:git+https://github.com/kt3k/ebean-enhance-plugin.git'
          }
          licenses {
            license {
              name 'MIT'
              url 'https://raw.github.com/kt3k/ebean-enhance-plugin/master/LICENSE'
              distribution 'repo'
            }
          }
          developers {
            developer {
              id 'kt3k'
              name 'Yoshiya Hinosawa'
              email 'stibium121@gmail.com'
            }
          }
        }
      }
    }
  }
}

signing {
  sign configurations.archives
}
